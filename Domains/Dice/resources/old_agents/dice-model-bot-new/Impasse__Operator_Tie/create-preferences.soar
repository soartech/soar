sp {selection*propose*create-preferences
   (state <s> ^name selection)
-->
   (<s> ^operator <op> + =, <)
   (<op> ^name create-preferences)
}

sp {selection*apply*create-preferences*convert*probability*numeric-preference
   (state <s> ^name selection
              ^operator.name create-preferences
             -^top-state.probabilities 0
              ^top-state.normalized-reward false
              ^superstate <ss>
              ^evaluation <e1>)
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^probability <nv>
         ^desired <d>)
-->
   (<ss> ^operator <x> = <nv>)}

sp {selection*apply*create-preferences*convert*probability*numeric-preference*normalized
   (state <s> ^name selection
              ^operator.name create-preferences
             -^top-state.probabilities 0
              ^top-state.normalized-reward true
              ^superstate <ss>
              ^evaluation <e1>)
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^probability <nv>
         ^desired <d>)
-->
   (<ss> ^operator <x> = (- (* 2 <nv>) 1))}

sp {selection*apply*create-preferences*convert*probability*numeric-preference*prob-0 #*normalized-false
   (state <s> ^name selection
              ^operator.name create-preferences
              ^top-state.probabilities 0
              #^top-state.normalized-reward false
              ^superstate <ss>
              ^evaluation <e1>)
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^probability <nv>
         ^desired <d>)
-->
   (<ss> ^operator <x> = 0.0)}

#sp {selection*apply*create-preferences*convert*probability*numeric-preference*prob-0*normalized-true
#   (state <s> ^name selection
#              ^operator.name create-preferences
#              ^top-state.probabilities 0
#              ^top-state.normalized-reward true
#              ^superstate <ss>
#              ^evaluation <e1>)
#   (<ss> ^desired <d>)
#   (<e1> ^superoperator <x>
#         ^probability <nv>
#         ^desired <d>)
#-->
#   (<ss> ^operator <x> = 0.5)}

#sp {selection*apply*create-preferences*save*probability
#   (state <s> ^name selection
#              ^operator.name create-preferences
#              ^superstate <ss>
#              ^evaluation <e1>)
#   (<ss> ^desired <d>)
#   (<e1> ^superoperator <sop>
#         ^ << symbolic-value probability >> <nv>
#         ^desired <d>)
#-->
#   (<sop> ^probability <nv>)}

sp {selection*apply*create-preferences*compare*bid-better-prior-bid # prefer last face for equal probability bids except next bid
   (state <s> ^name selection
             -^top-state.probabilities-only true
              ^operator.name create-preferences
             -^top-state.next-player-data.face <yface>             
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
   (<ss> ^desired <d>
         ^io.input-link.state.last-bid.face <face>)
   (<e1> ^superoperator <x>
         ^numeric-value >= <sv>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^numeric-value <sv>
         ^desired <d>)
   (<x> ^name bid
        ^face <face>
       -^push-face)
   (<y> ^name bid
        ^face { <yface> <> <face> })
-->
   (<ss> ^operator <x> > <y>)}

sp {selection*apply*create-preferences*compare*bid-better-next-bid # prefer next face for equal probability bids except last bid
   (state <s> ^name selection
             -^top-state.probabilities-only true
              ^operator.name create-preferences
              ^top-state.next-player-data.face <face>             
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
   (<ss> ^desired <d>
        -^io.input-link.state.last-bid.face <lface>)
   (<e1> ^superoperator <x>
         ^numeric-value >= <sv>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^numeric-value <sv>
         ^desired <d>)
   (<x> ^name bid
        ^face <face>
       -^push-face)
   (<y> ^name bid
        ^face { <lface> <> <face> })
-->
   (<ss> ^operator <x> > <y>)}

## Always prefer a bid to a challenge if it has a higher probability (don't take chances with a challenge).

sp {selection*apply*create-preferences*compare*bid-better-challenge
   (state <s> ^name selection
             -^top-state.probabilities-only true
              ^operator.name create-preferences
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
   (<ss> ^desired <d>)
   (<e1> ^superoperator <x>
         ^numeric-value >= <sv>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^numeric-value { <> 1.0 <sv> }
         ^desired <d>)
   (<x> ^name bid)
   (<y> ^name challenge)
-->
   (<ss> ^operator <x> > <y>)}

sp {selection*apply*create-preferences*prefer*best*bid*challenge*certain*no-model
   (state <s> ^name selection
              ^operator.name create-preferences
              ^superstate <ss>
              ^evaluation <e>)
   (<e> ^superoperator <so>
        ^use-model true
        ^symbolic-value certain)
   (<so> ^name challenge)
-->
   (<ss> ^operator <so> = .8)}


sp {selection*apply*create-preferences*compare*bid-better-prior-bid*symbolic # prefer last face for equal probability bids except next bid
   (state <s> ^name selection
              ^operator.name create-preferences
             -^top-state.expectation-only true
             -^top-state.next-player-data.face <yface>             
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
   (<ss> ^desired <d>
         ^io.input-link.state.last-bid.face <face>)
   (<e1> ^superoperator <x>
         ^symbolic-value <sv>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^symbolic-value <sv>
         ^desired <d>)
   (<x> ^name bid
        ^face <face>
       -^push-face)
   (<y> ^name bid
        ^face { <yface> <> <face> })
-->
   (<ss> ^operator <x> > <y>)}

sp {selection*apply*create-preferences*compare*bid-better-next-bid*symbolic # prefer next face for equal probability bids except last bid
   (state <s> ^name selection
              ^operator.name create-preferences
             -^top-state.expectation-only true
              ^top-state.next-player-data.face <face>             
              ^superstate <ss>
              ^evaluation <e1> { <> <e1> <e2> })
   (<ss> ^desired <d>
        -^io.input-link.state.last-bid.face <lface>)
   (<e1> ^superoperator <x>
         ^symbolic-value <sv>
         ^desired <d>)
   (<e2> ^superoperator { <> <x> <y> }
         ^symbolic-value <sv>
         ^desired <d>)
   (<x> ^name bid
        ^face <face>
       -^push-face)
   (<y> ^name bid
        ^face { <lface> <> <face> })
-->
   (<ss> ^operator <x> > <y>)}
